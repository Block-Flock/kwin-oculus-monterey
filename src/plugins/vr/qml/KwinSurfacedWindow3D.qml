/*
    SPDX-FileCopyrightText: 2026 Stanislav Aleksandrov <lightofmysoul@gmail.com>

    SPDX-License-Identifier: GPL-2.0-or-later
*/

import QtQuick
import QtQuick.Controls
import QtQuick.Layouts

import QtQuick3D
import QtQuick3D.Helpers
import QtQuick3D.Xr

import org.kde.kwin.vr

/* This is a KWin window composed from wayland surfaces and subsurfaces without server decorations
 * Each surface is a 3D rectangle. all of them are arranged as a stack by ZStacker.
 */
KwinWaylandSurface3D {
    id: root
    required client
    surface: root.client?.surface ?? null

    KwinWindowOffscreenRef {
        window: root.client?.vr ? root.client : null
    }

    Repeater3D {
        id: subSurfaceRepeater
        model: KwinWaylandSurfaceModel {
            id: ssDataModel
            surface: root.surface
        }
        delegate: KwinWaylandSubSurface3DRecursive {
            ppu: root.ppu
            client: root.client
            grabHandle: root.grabHandle
            nextComponent: subSurfaceRepeater.delegate
        }
    }

    property zMargins itemDepth: ({
                                      top: Math.max(rwa.depth.top, root.visible ? KWinVRConfig.zWindowMarginTop : 0),
                                      bottom: Math.max(rwa.depth.bottom, root.visible ? KWinVRConfig.zWindowMarginBottom : 0)
                                  })
    // onItemDepthChanged: console.log("---> item Depth (main)", itemDepth)
    ZStacker {
        id: rwa
        target: subSurfaceRepeater
        initialMargins: root.surfaceModelDepth
        centerIndex: root.surfaceIndex
        globalOffset: root.zOffsetGlobal
    }

}
